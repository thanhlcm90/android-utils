apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def releaseVersion = '2.8.0'
def bintrayUser = hasProperty('bintray_user') ? bintray_user : ''
def bintrayApiKey = hasProperty('bintray_apikey') ? bintray_apikey : ''

android
{
    compileSdkVersion 27
    buildToolsVersion '27.0.1'

    dexOptions
    {
        javaMaxHeapSize '2g'
    }

    defaultConfig
    {
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName releaseVersion
    }

    buildTypes
    {
        debug
        {
            debuggable true
        }

        release
        {
            minifyEnabled false
        }
    }

    lintOptions
    {
        abortOnError false
    }

    compileOptions
    {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

group = 'com.mauriciotogneri'
version = releaseVersion

bintray
{
    user = bintrayUser
    key = bintrayApiKey
    configurations = ['archives']
    pkg
    {
        repo = 'maven'
        name = 'androidutils'
        licenses = ['MIT']
        publish = true
        version
        {
            name = releaseVersion
            vcsTag = releaseVersion
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

ext
{
    support = '27.0.1'
}

dependencies
{
    // android
    compile "com.android.support:appcompat-v7:$support"
    compile "com.android.support:support-annotations:$support"
    compile "com.android.support:customtabs:$support"

    // gson
    compile 'com.google.code.gson:gson:2.8.2'

    // joda time
    compile 'joda-time:joda-time:2.9.9'

    // java utils
    compile 'com.mauriciotogneri:javautils:1.8.0'

    // event bus
    compile 'org.greenrobot:eventbus:3.0.0'

    // aes crypto
    compile 'com.scottyab:aes-crypto:0.0.4'
}